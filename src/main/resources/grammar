Keyword :: one of
  `and` `as` `assert` `await` `constraint` `else` `exception` `external` `false` `for` `if` `in` `include` `lazy` `let` `list` `module` `mutable` `of` `open` `private` `rec` `switch` `true` `try` `type` `when` `while`

Letter ::
  Lletter
  Uletter

Lletter :: one of
  `a` `b` `c` `d` `e` `f` `g` `h` `i` `j` `k` `l` `m` `n` `o` `p` `q` `r` `s` `t` `u` `v` `w` `x` `y` `z`

Uletter :: one of
  `A` `B` `C` `D` `E` `F` `G` `H` `I` `J` `K` `L` `M` `N` `O` `P` `Q` `R` `S` `T` `U` `V` `W` `X` `Y` `Z`

DecimalDigit :: one of
  `0` `1` `2` `3` `4` `5` `6` `7` `8` `9`

DecimalDigits ::
  DecimalDigit
  DecimalDigits DecimalDigit
  DecimalDigits `_`

HexDigit :: one of
  `0` `1` `2` `3` `4` `5` `6` `7` `8` `9` `a` `b` `c` `d` `e` `f` `A` `B` `C` `D` `E` `F`

HexDigits ::
  HexDigit
  HexDigits HexDigit
  HexDigits `_`

Lident ::
  LidentName but not Keyword

LidentName ::
  LidentFirst
  LidentName IdentRest

LidentFirst ::
  Lletter
  `_`

Uident ::
  UidentName

UidentName ::
  UidentFirst
  UidentName IdentRest

UidentFirst ::
  Uletter

IdentRest ::
  Letter
  DecimalDigit
  `_`
  `'`

Integer[Suffix] ::
  IntegerLiteral
  [+Suffix] IntegerLiteral suffix

Float[Suffix] ::
  FloatLiteral
  [+Suffix] FloatLiteral suffix

// This is from JavaScript String
String ::
  `"` StringCharacters? `"`

StringCharacters ::
  StringCharacter StringCharacters?

StringCharacter ::
  SourceCharacter but not `"` `\` LineTerminator

SourceCharacter ::
  > any Unicode code point

LineTerminator ::
  <LF>
  <CR>
  <LS>
  <PS>

IntegerLiteral ::
  NonHexIntegerLiteral
  HexIntegerLiteral

NonHexIntegerLiteral ::
  DecimalLiteral
  BinaryIntegerLiteral
  OctalIntegerLiteral

FloatLiteral ::
  NonHexIntegerLiteral DecimalFractional
  NonHexIntegerLiteral DecimalExponent
  NonHexIntegerLiteral DecimalFractional DecimalExponent
  HexIntegerLiteral HexFractional
  HexIntegerLiteral HexExponent
  HexIntegerLiteral HexFractional HexExponent

DecimalFractional ::
  `.` DecimalDigits

HexFractional ::
  `.` HexDigits

DecimalExponent ::
  ExponentChar DecimalDigits
  ExponentChar `+` DecimalDigits
  ExponentChar `-` DecimalDigits

HexExponent ::
  ExponentChar HexDigits
  ExponentChar `+` HexDigits
  ExponentChar `-` HexDigits

ExponentChar :: one of
  `e` `E` `p` `P`

DecimalLiteral ::
  DecimalDigits

BinaryIntegerLiteral ::
  `0b` DecimalDigits
  `0B` DecimalDigits

OctalIntegerLiteral ::
  `0o` DecimalDigits
  `0O` DecimalDigits

HexIntegerLiteral ::
  `0x` HexDigits
  `0X` HexDigits

Suffix :: one of
  `g` `h` `i` `j` `k` `l` `m` `o` `p` `q` `r` `s` `t` `u` `v` `w` `x` `y` `z` `G` `H` `I` `J` `K` `L` `M` `N` `O` `P` `Q` `R` `S` `T` `U` `V` `W` `X` `Y` `Z`

Specifications :
  Specification
  Specification `;` Specifications 

Implementations :
  Implementation
  Implementation `;` Implementations

Specification :
  SignLetDescription
  TypeDefinition
  TypeExtension
  External
  ExceptionDefinition
  OpenDescription
  IncludeModuleType
  ModuleSpecification
  StandaloneAttribute
  ExtensionModuleLevel
  Attribute Specification

NonRecFlag:
  `nonrec`

TypeDefinition :
  `type` `rec` TypeDefs
  `type` NonRecFlag? TypeDefs

TypeDefs :
  TypeDef
  TypeDef TypeDefRest

TypeDefRest :
  `and` TypeDefs
  Attribute TypeDefRest

TypeDef :
  TypeConstrName AngleBraketTypeParams? TypeInfomation?

TypeInfomation :
  `=` TypeEquationAndRepresentation[+Private]
  TypeInfomation TypeConstraints

TypeConstraints :
  TypeConstraint
  TypeConstraint TypeConstraints

TypeEquationAndRepresentation[Private] :
  TypeEquationOrConstrDecl
  RecordOrObjectDecl
  TypeRepresentation[~Private]
  TypeExpression[+Es6Arrow, +Alias]
  [+Private] TypeExpression[+Es6Arrow, +Alias] `=` TypeRepresentation[+Private]
  [+Private]`private` TypeEquationAndRepresentation[~Private]

TypeConstrName :
  Lident

TypeConstraint :
  `constraint` TypeLVar `=` TypeExpression[+Es6Arrow, +Alias]

//TODO
Implementation :
  [empty]

TypeAlias :
  `as` TypeLVar

TypeEquationOrConstrDecl :
  ValuePath TypeConstructorArgs TypeAlias?
  Uident ConstrDeclArgs TypeConstructorDeclarations
  Uident ConstrDeclArgs `|` TypeConstructorDeclarations

ConstrDeclArgs :
  [empty]
  CoverParenthesizedTypeExpressions
  `(` RecordOrObjectType TypeExpressions? `)`
  `(` RecordOrObjectType `,` TypeExpressions? `)`
  `(` `{` `...` TypeExpression[+Es6Arrow, +Alias] `,` StringFieldDeclarations? `}` TypeAlias? ArrowTypeRest[+Es6Arrow]  TypeExpressions? `)`
  `(` `{` `...` TypeExpression[+Es6Arrow, +Alias] `,` StringFieldDeclarations? `}` TypeAlias? ArrowTypeRest[+Es6Arrow] `,` TypeExpressions? `)`
  `(` `{` AttributedStringFieldDeclarations `}` `)` TypeAlias? ArrowTypeRest[+Es6Arrow]
  `(` `{` AttributedStringFieldDeclarations `}` `,` `)` TypeAlias? ArrowTypeRest[+Es6Arrow]
  `(` `{` AttributedFieldDeclarations `}` `,` `)`
  ConstrDeclArgs `:` TypeExpression[+Es6Arrow, +Alias]

TypeConstructorDeclarations :
  TypeConstructorDeclaration
  TypeConstructorDeclaration `|` TypeConstructorDeclarations

RecordOrObjectDecl :
  RecordOrObjectType TypeAlias? ArrowTypeRest[+Es6Arrow]
  `{` `...` TypeExpression[+Es6Arrow, +Alias] `,` StringFieldDeclarations? `}` TypeAlias? ArrowTypeRest[+Es6Arrow]
  `{` AttributedStringFieldDeclarations `}` TypeAlias? ArrowTypeRest[+Es6Arrow]
  `{` AttributedStringFieldDeclarations `}` `,` TypeAlias? ArrowTypeRest[+Es6Arrow]
  `{` AttributedFieldDeclarations `}`

TypeConstructorDeclaration :
  Uident ConstrDeclArgs
  Attribute TypeConstructorDeclaration

TypeRepresentation[Private] :
  `|` TypeConstructorDeclarations
  `..`
  TypeConstructorDeclarations
  [+Private] `private` TypeRepresentation[~Private]

TypeExtension :
  `type` `rec` TypeExtensionLHS `+=` ConstrDefinitions
  `type` `rec` TypeExtensionLHS `+=` `private` ConstrDefinitions
  `type` NonRecFlag? TypeExtensionLHS `+=` ConstrDefinitions
  `type` NonRecFlag? TypeExtensionLHS `+=` `private` ConstrDefinitions

TypeExtensionLHS :
  ValuePath TypeParams

ConstrDefinitions :
  ConstrDefinition[+Attr]
  `|` ConstrDefinition[+Attr]
  ConstrDefinitions `|` ConstrDefinition[+Attr]

ConstrDefinition[Attr] :
  Uident ConstrDeclArgs
  Uident `=` ModulePath
  Uident `:` TypeExpression[+Es6Arrow, +Alias]
  [+Attr] Attribute ConstrDefinition[+Attr]

External :
  `external` Lident `:` TypeExpression `=` String

ExceptionDefinition :
  `exception` ConstrDefinition[~Attr]

OpenDescription :
  `open` ModulePath
  `open` `!` ModulePath

IncludeModuleType :
  `include` ModuleType[+Es6Arrow,+With]

ModuleSpecification :
  [empty]

StandaloneAttribute :
  `@@` AttributeId
  `@@` AttributeId Payload

ExtensionModuleLevel :
  `%%` AttributeId
  `%%` AttributeId Payload

Ident :
  Uident
  Lident

SignLetDescription :
  `let` Lident `:` PolyTypeExpression

PolyTypeExpression :
  TypeExpression[+Es6Arrow, +Alias]
  TypeLVars `.` TypeExpression[+Es6Arrow, +Alias]
  TypeLVar `=>` TypeExpression[+Es6Arrow, ~Alias]

TypeVar :
  TypeLVar
  TypeUVar

TypeLVar :
  `'` Lident

TypeUVar :
  `'` Uident

TypeLVars :
  TypeLVar
  TypeLVar TypeLVars

TypeExpression[Es6Arrow, Alias] :
  [+Es6Arrow] Es6ArrowType
  AtomicTypeExpression ArrowTypeRest[?Es6Arrow]
  [+Alias] TypeExpression[?Es6Arrow, ?Alias] TypeAlias
  Attribute TypeExpression[?Es6Arrow, ?Alias]

Es6ArrowType :
  [empty]
  Es6ArrowLHS ArrowTypeRest[+Es6Arrow]

Es6ArrowLHS :
  `~` Lident `:` TypeExpression[~Es6Arrow, ~Alias]
  `~` Lident `:` TypeExpression[~Es6Arrow, ~Alias] `=` `?`
  CoverParenthesizedTypeParameters

CoverParenthesizedTypeParameters :
  `(` `)`
  `(` TypeParameters `)`

AngleBraketTypeParams :
  `<` `>`
  `<` TypeParams `>`

TypeParameters :
  TypeParameter
  TypeParameter `,` TypeParameters

TypeParams :
  TypeParam
  TypeParam `,` TypeParam
  
TypeParameter :
  TypeExpression[+Es6Arrow, +Alias]
  `~` Lident `:` TypeExpression[+Es6Arrow, ~Alias]
  `~` Lident `:` TypeExpression[+Es6Arrow, ~Alias] `=` `?`
  Lident TypeConstructorArgs? ArrowTypeRest[+Es6Arrow]
  `.` TypeParameter
  Attribute TypeParameter

Variance :
  [empty]
  `+`
  `-`

TypeParam :
  Variance TypeVar
  Variance `_`

ArrowTypeRest[Es6Arrow] :
  [empty]
  [+Es6Arrow] `=>` TypeExpression[+Es6Arrow, ~Alias]

TypeConstructorArgs :
  `<` `>`
  `<` TypeExpressions `>`

// TODO
AtomicTypeExpression :
  TypeVar
  `_`
  CoverParenthesizedTypeExpressions
  PolymorphicVariantType
  ValuePath TypeConstructorArgs
  `module` `(` PackageType `)`
  Extension
  RecordOrObjectType

CoverParenthesizedTypeExpressions :
  `(` `)`
  `(` TypeExpressions `)`

TypeExpressions :
  TypeExpression[+Es6Arrow, +Alias]
  TypeExpression[+Es6Arrow, +Alias] `,` TypeExpressions 

AttributedStringFieldDeclarations :
  StringFieldDeclarations
  Attribute StringFieldDeclarations

StringFieldDeclarations :
  StringFieldDeclaration
  StringFieldDeclaration `,` StringFieldDeclarations

AttributedFieldDeclarations :
  FieldDeclarations
  Attribute FieldDeclarations

FieldDeclarations :
  FieldDeclaration
  FieldDeclaration `,` FieldDeclarations

OptionalLabel :
  `?`

FieldDeclaration :
  Lident OptionalLabel?
  Lident OptionalLabel? `:` PolyTypeExpression
  `mutable` Lident OptionalLabel?
  `mutable` Lident OptionalLabel? `:` PolyTypeExpression
  Attribute FieldDeclaration

StringFieldDeclaration :
  String `:` PolyTypeExpression
  `...` TypeExpression[+Es6Arrow, +Alias]
  Attribute StringFieldDeclaration

OpenFlag :
  `..`

ClosedFlag :
  `.`

RecordOrObjectType :
  OpenRecordOrObjectType
  ClosedRecordOrObjectType

OpenRecordOrObjectType :
  `{` OpenFlag `}`
  `{` OpenFlag StringFieldDeclarations `}`

ClosedRecordOrObjectType :
  `{` ClosedFlag? `}`
  `{` ClosedFlag? StringFieldDeclarations `}`

PolymorphicVariantType :
  `[` `>` TagSpec? TagSpecs? `]`
  `[` `<` TagSpecFull TagSpecFulls? TagNames? `]`
  `[` `<` `|` TagSpecFull TagSpecFulls? TagNames? `]`
  `[` TagSpecFirst TagSpecs? `]`

TagSpec :
  PolymorphicVariantTypeSpecHash[~Full]
  TypeExpression[+Es6Arrow, +Alias]
  Attribute TagSpec

TagSpecFull :
  PolymorphicVariantTypeSpecHash[+Full]
  TypeExpression[+Es6Arrow, +Alias]
  Attribute TagSpecFull

TagSpecFirst :
  `|` TagSpec
  PolymorphicVariantTypeSpecHash[~Full]
  TypeExpression[+Es6Arrow, +Alias]
  Attribute TagSpecFirst

PolymorphicVariantTypeSpecHash[Full] :
  HashIdent
  PolymorphicVariantTypeSpecHash[?Full] PolymorphicVariantTypeArgs
  [+Full] PolymorphicVariantTypeSpecHash[?Full] `&` PolymorphicVariantTypeArgs

PolymorphicVariantTypeArgs :
  CoverParenthesizedTypeExpressions

HashIdent :
  `#` Ident
  `#` Integer[~Suffix]

TagNames :
  `>`
  TagNames TagName

TagName :
  HashIdent

TagSpecs :
  `|` TagSpec
  TagSpecs `|` TagSpec

TagSpecFulls :
  `|` TagSpecFull
  TagSpecFulls `|` TagSpecFull

Attribute :
  `@` AttributeId
  `@` AttributeId Payload

AttributeId :
  AttributeIdElem
  AttributeIdElem `.` AttributeId

AttributeIdElem :
  Keyword
  Ident

Payload :
  `(` `)`
  `(` `:` `)`
  `(` `:` Specifications `)`
  `(` `:` TypeExpression[+Es6Arrow, +Alias] `)`
  `(` `?` Pattern[+Alias, +Or] `)`
  `(` `?` Pattern[+Alias, +Or] `when` Expression_OrdExprContext `)`
  `(` `?` Pattern[+Alias, +Or] `if` Expression_OrdExprContext `)`
  `(` Implementations `)`

PatternGuard :
  [empty] 
  `when` Expression_WhenExprContext
  `if` Expression_WhenExprContext

Pattern[Alias, Or] :
  `true`
  `false`
  Constant
  Interval
  TemplateConstant
  TuplePattern
  ArrayPattern
  RecordPattern
  `_`
  Lident TemplateConstant?
  ModulePath ConstructorPatternArgs?
  TagName
  `#...` ValuePath
  HashIdent
  `exception` Pattern[~Alias, ~Or]
  `lazy` Pattern[~Alias, ~Or]
  `list` ListPattern
  `module` ModulePattern
  Extension
  Attribute Pattern[?Alias, ?Or]
  [+Alias] Pattern[?Alias, ?Or] TypeAlias
  [+Or] Pattern[?Alias, ?Or] `|` Pattern[?Alias, ~Or]

//TODO : String, Codepoint, +-
Constant :
  Integer[+Suffix]
  `+` Integer[+Suffix]
  `-` Integer[+Suffix]
  Float[+Suffix]
  `+` Float[+Suffix]
  `-` Float[+Suffix]
  String

Interval :
  Constant `..` Constant

//TODO
TemplateConstant :
  [empty]

ConstrainedPatterns :
  ConstrainedPattern
  ConstrainedPattern `,` ConstrainedPatterns

RecordPatternRows :
  RecordPatternRow
  RecordPatternRow `,` RecordPatternRows

TuplePattern :
  `(` `)`
  `(` ConstrainedPatterns `)`

ArrayPattern :
  `[` `]`
  `[` ConstrainedPatterns `]`

RecordPattern :
  `{` `}`
  `{` RecordPatternRows `}`

ConstructorPatternArgs :
  `(``)`
  `(` ConstrainedPatterns `)`

ListPattern :
  `{` `}`
  `{` ConstrainedPatterns `}`
  `{` `...` ConstrainedPatterns `}`

ModulePattern :
  `(` `)`
  `(` Uident `)`
  `(` Uident `:` PackageTypeWithAttr `)`

ModuleType[Es6Arrow, With] :
  [+Es6Arrow] Es6ArrowFunctor
  AtomicModuleType
  Attribute ModuleType[?Es6Arrow, ?With]

Es6ArrowFunctor :
  `(` FunctorArgs? `)` `=>` ModuleType
  Attribute Es6ArrowFunctor

FunctorArgs :
  FunctorArg
  FunctorArg FunctorArgs
  
FunctorArg :
  Uident `:` ModuleType[+Es6Arrow, +With]
  ModulePath
  `_` `:` ModuleType[+Es6Arrow, +With]
  `(` `)`
  Attribute FunctorArg

AtomicModuleType :
  ModuleOrValuePath
  `(` ModuleType[+Es6Arrow, +With] `)`
  `{` Specifications `}`
  `module` `type` `of` ModuleExpression
  Extension

ModuleExpression :
  FunctorModuleExpr
  PrimaryModExpr
  Attribute ModuleExpression

PrimaryModExpr :
  AtomicModuleExpr ModuleApplications?

ModuleApplications :
  `(` ModuleApplication? `)`
  `(` ModuleApplication? `)` ModuleApplications

ModuleApplication :
  ConstrainedModExpr
  ConstrainedModExpr `,` ModuleApplication

ConstrainedModExpr :
  ModuleExpression
  ModuleExpression `:` ModuleType[+Es6Arrow, +With]

PackageTypeWithAttr :
  PackageType
  Attribute PackageType

PackageType :
  ModuleOrValuePath
  ModuleOrValuePath `with` PackageConstraints

PackageConstraints :
  PackageConstraint
  PackageConstraint `and` PackageConstraints
  
PackageConstraint :
  `type` ValuePath `=` TypeExpression[+Es6Arrow, +Alias]

Extension :
  `%` AttributeId
  `%` AttributeId Payload

ConstrainedPattern :
  Pattern[+Alias, +Or]
  Pattern[+Alias, +Or] `:` TypeExpression[+Es6Arrow, +Alias]

RecordPatternRow :
  `...` RecordPatternRowField
  RecordPatternRowField
  `?` RecordPatternRowField
  `_`
  Attribute RecordPatternRow

ModuleName :
  Uident

ModulePath :
  ModuleName
  ModuleName `.` ModulePath

ValueName :
  Lident

ValuePath :
  ValueName
  ModulePath `.` ValueName

ModuleOrValuePath :
  ValueName
  ModulePath
  ModulePath `.` ValueName

RecordPatternRowField :
  ValuePath 
  ValuePath `:` Pattern[+Alias, +Or]
  ValuePath `:` `?` Pattern[+Alias, +Or]

Expression_OrdExprContext :
  [empty]

Expression_WhenExprContext :
  [empty]
